<!DOCTYPE html><html lang="en"><head>
  <meta charset="UTF-8">
  <link rel="icon" type="image/svg+xml" href="/src/favicon.svg">
  <meta name="viewport" content="width=device-width, initial-scale=1.0">
  <title></title>
  <script src="https://ajax.googleapis.com/ajax/libs/webfont/1.6.26/webfont.js"></script>
  <script>
    document.documentElement.className = "js";
    var supportsCssVars = function() { var e, t = document.createElement("style"); return t.innerHTML = "root: { --tmp-var: bold; }", document.head.appendChild(t), e = !!(window.CSS && window.CSS.supports && window.CSS.supports("font-weight", "var(--tmp-var)")), t.parentNode.removeChild(t), e };
    supportsCssVars() || alert("Please view this demo in a modern browser that supports CSS Variables.");
    </script>
  <script crossorigin="">import('/demo-react-2/assets/index.80708aec.js').finally(() => {
            
    const qiankunLifeCycle = window.moudleQiankunAppLifeCycles && window.moudleQiankunAppLifeCycles['demo-react-2'];
    if (qiankunLifeCycle) {
      window.proxy.vitemount((props) => qiankunLifeCycle.mount(props));
      window.proxy.viteunmount((props) => qiankunLifeCycle.unmount(props));
      window.proxy.vitebootstrap(() => qiankunLifeCycle.bootstrap());
      window.proxy.viteupdate((props) => qiankunLifeCycle.update(props));
    }
  
          })</script>
  <link rel="stylesheet" href="/demo-react-2/assets/index.555bf9c7.css">
</head>

<body>
  <div id="demo-react-1-root"></div>
  <script type="x-shader/x-fragment" id="Image">
    precision highp float;
  
    uniform vec2  iResolution;
    uniform float iTime;
    // ... other needed uniforms
   
    // -- Paste your Shadertoy code here:
  
    float n21 (vec2 p)
    {
        return fract(sin(dot(p, vec2(12.9898, 78.233))) * 43758.5453);
    }
  
  
    vec2 n22 (vec2 p)
    {
        float n = n21(p);
        return vec2(n, n21(p+n));
    }
  
  
    mat2 rotate (float a) 
    {
        float s = sin(a);
        float c = cos(a);
        return mat2(c, -s, s, c);
    }
  
  
    vec3 diffuseLight (vec2 uv, vec3 normals, vec3 pos, vec3 col)
    {
        vec3 dif = pos - vec3(uv, 0.); // point light
        //vec3 dif = pos; // directional light
        vec3 dir = normalize(dif);
        float intensity = 1. / length(dif); // inverse square law
        float diffuse   = dot(normals, dir) * intensity;
        return col * diffuse;
    }
  
    #define PI 3.14159265358979323846
    #define RATIO iResolution.x / iResolution.y
    #define POINTS 6
    #define FORCE 2. // the larger the smaller the swirl
    #define ROTATION 4.
  
  
    // swirl uv at random point
    vec2 swirl (vec2 uv, float seed, inout float acc_frc, inout vec2 acc_rot)
    {
        float tm = iTime * .05;
        
        // point
        float n = n21(vec2(seed));
        vec2 pnt = vec2(.5) + vec2(cos(tm + n * 423.1), sin(tm + n * 254.3) * .5);
  
        // rotate point
        vec2  dif = uv - pnt;
        float dis = length(dif);
        float frc = smoothstep(.0, 1., exp(dis * -FORCE) * (cos(dis * 10.) * .5 + .5));
        float swl = frc * sin(tm + n * 624.8) * ROTATION; 
        vec2  rot = dif * rotate(swl);
  
        // for normal map
        acc_frc += frc;
        acc_rot += rot * frc; 
  
        // rotated uv
        return rot + pnt;
    }
  
  
    vec3 offsetLights (vec2 uv, vec3 normals)
    {
        float tm  = iTime * .1;
        float off = 1.3; // offset
        float sth = .5; // strength
        float amb = .5; // ambient
        float rad = 1.; // radius
        vec3 pos  = vec3(.5, .5, 1.);
        
        float o2 = 1. * off;
        float o3 = 2. * off;
        
        vec3 p1 = vec3(pos.x + cos(tm) * rad, pos.y + sin(tm + 0.) * rad, pos.z);
        vec3 p2 = vec3(pos.x + cos(tm + o2) * rad, pos.y + sin(tm + o2) * rad, pos.z);
        vec3 p3 = vec3(pos.x + cos(tm + o3) * rad, pos.y + sin(tm + o3) * rad, pos.z);
        
        vec3 l1 = diffuseLight(uv, normals, p1, vec3(sth, 0., 0.));
        vec3 l2 = diffuseLight(uv, normals, p2, vec3(0., sth, 0.));
        vec3 l3 = diffuseLight(uv, normals, p3, vec3(0., 0., sth));
        
        return l1 + l2 + l3 + amb;
    }
  
  
    void mainImage( out vec4 fragColor, in vec2 fragCoord )
    {
        vec2 uv = (fragCoord / iResolution.xy - .5) * vec2(RATIO, 1.) + .5;
        vec3 col = vec3(0.);
        float tm = iTime;
          
        // reclusively swirl th uv
        float acc_frc = .0;
        vec2  acc_rot = vec2(0.);
        vec2  sv = uv;
        for (int i = 0; i < POINTS; i++)
            sv = swirl(sv, fract(float(i+1) * 123.45), acc_frc, acc_rot);
  
        // normal map
        vec3 normal = normalize(vec3(acc_rot, acc_frc * .01));
        
        // light
        vec3 diffuse   = offsetLights(uv, normal);
        vec3 specular  = smoothstep(.85, .95, diffuse);
        vec3 light     = diffuse + specular;
        
        //col = light;
        //col = normal;
        col = light + sin(light * PI) - light; // Let's make it a bit more interesting
    
        fragColor = vec4(col,1.0);
    }
  
    // -- End of Shadertoy code
      
      
    void main() {
      mainImage(gl_FragColor, gl_FragCoord.xy);
    }
  </script>
  <script id="vertexShader" type="x-shader/x-vertex">//
    // GLSL textureless classic 3D noise "cnoise",
    // with an RSL-style periodic variant "pnoise".
    // Author:  Stefan Gustavson (stefan.gustavson@liu.se)
    // Version: 2011-10-11
    //
    // Many thanks to Ian McEwan of Ashima Arts for the
    // ideas for permutation and gradient selection.
    //
    // Copyright (c) 2011 Stefan Gustavson. All rights reserved.
    // Distributed under the MIT license. See LICENSE file.
    // https://github.com/ashima/webgl-noise
    //
    
    vec3 mod289(vec3 x)
    {
      return x - floor(x * (1.0 / 289.0)) * 289.0;
    }
    
    vec4 mod289(vec4 x)
    {
      return x - floor(x * (1.0 / 289.0)) * 289.0;
    }
    
    vec4 permute(vec4 x)
    {
      return mod289(((x*34.0)+1.0)*x);
    }
    
    vec4 taylorInvSqrt(vec4 r)
    {
      return 1.79284291400159 - 0.85373472095314 * r;
    }
    
    vec3 fade(vec3 t) {
      return t*t*t*(t*(t*6.0-15.0)+10.0);
    }
    
    // Classic Perlin noise
    float cnoise(vec3 P)
    {
      vec3 Pi0 = floor(P); // Integer part for indexing
      vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
      Pi0 = mod289(Pi0);
      Pi1 = mod289(Pi1);
      vec3 Pf0 = fract(P); // Fractional part for interpolation
      vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
      vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
      vec4 iy = vec4(Pi0.yy, Pi1.yy);
      vec4 iz0 = Pi0.zzzz;
      vec4 iz1 = Pi1.zzzz;
    
      vec4 ixy = permute(permute(ix) + iy);
      vec4 ixy0 = permute(ixy + iz0);
      vec4 ixy1 = permute(ixy + iz1);
    
      vec4 gx0 = ixy0 * (1.0 / 5.0);
      vec4 gy0 = fract(floor(gx0) * (1.0 / 5.0)) - 0.5;
      gx0 = fract(gx0);
      vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
      vec4 sz0 = step(gz0, vec4(0.0));
      gx0 -= sz0 * (step(0.0, gx0) - 0.5);
      gy0 -= sz0 * (step(0.0, gy0) - 0.5);
    
      vec4 gx1 = ixy1 * (1.0 / 5.0);
      vec4 gy1 = fract(floor(gx1) * (1.0 / 5.0)) - 0.5;
      gx1 = fract(gx1);
      vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
      vec4 sz1 = step(gz1, vec4(0.0));
      gx1 -= sz1 * (step(0.0, gx1) - 0.5);
      gy1 -= sz1 * (step(0.0, gy1) - 0.5);
    
      vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
      vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
      vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
      vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
      vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
      vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
      vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
      vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);
    
      vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
      g000 *= norm0.x;
      g010 *= norm0.y;
      g100 *= norm0.z;
      g110 *= norm0.w;
      vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
      g001 *= norm1.x;
      g011 *= norm1.y;
      g101 *= norm1.z;
      g111 *= norm1.w;
    
      float n000 = dot(g000, Pf0);
      float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
      float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
      float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
      float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
      float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
      float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
      float n111 = dot(g111, Pf1);
    
      vec3 fade_xyz = fade(Pf0);
      vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
      vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
      float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
      return 2.2 * n_xyz;
    }
    
    // Classic Perlin noise, periodic variant
    float pnoise(vec3 P, vec3 rep)
    {
      vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
      vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
      Pi0 = mod289(Pi0);
      Pi1 = mod289(Pi1);
      vec3 Pf0 = fract(P); // Fractional part for interpolation
      vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
      vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
      vec4 iy = vec4(Pi0.yy, Pi1.yy);
      vec4 iz0 = Pi0.zzzz;
      vec4 iz1 = Pi1.zzzz;
    
      vec4 ixy = permute(permute(ix) + iy);
      vec4 ixy0 = permute(ixy + iz0);
      vec4 ixy1 = permute(ixy + iz1);
    
      vec4 gx0 = ixy0 * (1.0 / 5.0);
      vec4 gy0 = fract(floor(gx0) * (1.0 / 5.0)) - 0.5;
      gx0 = fract(gx0);
      vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
      vec4 sz0 = step(gz0, vec4(0.0));
      gx0 -= sz0 * (step(0.0, gx0) - 0.5);
      gy0 -= sz0 * (step(0.0, gy0) - 0.5);
    
      vec4 gx1 = ixy1 * (1.0 / 5.0);
      vec4 gy1 = fract(floor(gx1) * (1.0 / 5.0)) - 0.5;
      gx1 = fract(gx1);
      vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
      vec4 sz1 = step(gz1, vec4(0.0));
      gx1 -= sz1 * (step(0.0, gx1) - 0.5);
      gy1 -= sz1 * (step(0.0, gy1) - 0.5);
    
      vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
      vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
      vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
      vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
      vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
      vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
      vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
      vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);
    
      vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
      g000 *= norm0.x;
      g010 *= norm0.y;
      g100 *= norm0.z;
      g110 *= norm0.w;
      vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
      g001 *= norm1.x;
      g011 *= norm1.y;
      g101 *= norm1.z;
      g111 *= norm1.w;
    
      float n000 = dot(g000, Pf0);
      float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
      float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
      float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
      float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
      float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
      float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
      float n111 = dot(g111, Pf1);
    
      vec3 fade_xyz = fade(Pf0);
      vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
      vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
      float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
      return 1.5 * n_xyz;
    }
    
    // Turbulence By Jaume Sanchez => https://codepen.io/spite/
    
    varying vec2 vUv;
    varying float noise;
    varying float qnoise;
    varying float displacement;
    
    uniform float time;
    uniform float displace;
    uniform float pointscale;
    uniform float decay;
    uniform float size;
    uniform float complex;
    uniform float waves;
    uniform float eqcolor;
    uniform bool fragment;
    
    float turbulence( vec3 p) {
      float t = -0.005;
      for(float f = 1.0 ; f <= 1.0 ; f++ ){
        float power = pow( 1.3, f );
        t += abs( pnoise( vec3( power * p ), vec3( 10.0, 10.0, 10.0 ) ) / power );
      }
      return t;
    }
    
    void main() {
    
      vUv = uv;
    
      noise = (2.0 *  - waves) * turbulence( decay * abs(normal + time));
      qnoise = (0.3 *  - eqcolor) * turbulence( decay * abs(normal + time));
      float b = pnoise( complex * (position) + vec3( (decay * 2.0) * time ), vec3( 100.0 ) );
      
      displacement = - atan(noise) + tan(b * displace);
    
      vec3 newPosition = (position) + (normal * displacement);
      gl_Position = (projectionMatrix * modelViewMatrix) * vec4( newPosition, abs(size) );
      gl_PointSize = (3.0);
    }
    </script>
    <script id="fragmentShader" type="x-shader/x-vertex">varying float qnoise;
    varying float noise;
    
    uniform float time;
    uniform bool redhell;
    uniform float rcolor;
    uniform float gcolor;
    uniform float bcolor;
    
    void main() {
      float r, g, b;
      
      if (!redhell == true) {
        r = sin(qnoise + rcolor);
        g = normalize(qnoise + (gcolor / 2.0));
        b = tan(qnoise + bcolor);
      } else {
        r = normalize(qnoise + rcolor);
        g = cos(qnoise + gcolor);
        b = sin(qnoise + bcolor);
      }
      gl_FragColor = vec4(r, g, b, 1.0);
    }</script>
  


<script>
  const createDeffer = (hookName) => {
    const d = new Promise((resolve, reject) => {
      window.proxy && (window.proxy[`vite${hookName}`] = resolve)
    })
    return props => d.then(fn => fn(props));
  }
  const bootstrap = createDeffer('bootstrap');
  const mount = createDeffer('mount');
  const unmount = createDeffer('unmount');
  const update = createDeffer('update');

  ;(global => {
    global.qiankunName = 'demo-react-2';
    global['demo-react-2'] = {
      bootstrap,
      mount,
      unmount,
      update
    };
  })(window);
</script></body></html>